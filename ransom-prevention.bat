@echo off

:: Inspired by bantya and his gist at https://gist.github.com/bantya/f1796317490cbc8d1264565245488e97
:: Simple way to prevent an infection with Bad Rabbit or Petya/NotPetya

TITLE Ransomware Prevention
Color 0C
SET v=1.0.1

net session>nul 2>&1
    if NOT %errorLevel% == 0 (
        echo.
		echo # You need admin rights for this to work!
		echo # Right-click this file and choose "Run as administrator".
		echo.
		echo # Press any key to exit...
		pause>NUL
		exit
    )

:start
Color 0F
cls
echo.
echo Script for BadRabbit and Petya/NotPetya prevention. Version %v%, 2017.
echo Visit https://github.com/phoenix1747/ransom-prevention/ for updates.
echo.
echo.
echo # This will create some files so that Bad Rabbit or Petya/NotPetya will be
echo # prevented from installing on this computer.
echo.
echo.
echo.
echo # What do you want to do? You can (i)nstall or (r)emove the needed files.
SET /P ANSWER=# Would you like to continue? (i/r): 
if /i %ANSWER%==i goto INSTALL
if /i %ANSWER%==r goto REMOVE
goto unrecog

:INSTALL
:: BadRabbit
echo This is not an empty file. Generated by ransom-prevention.bat > %windir%\cscc.dat
echo This is not an empty file. Generated by ransom-prevention.bat > %windir%\infpub.dat
icacls "%windir%\cscc.dat" /inheritance:r /remove *S-1-5-32-544
icacls "%windir%\infpub.dat" /inheritance:r /remove *S-1-5-32-544
:: Petya and NotPetya
echo This is not an empty file. Generated by ransom-prevention.bat > %windir%\perfc.dll
echo This is not an empty file. Generated by ransom-prevention.bat > %windir%\perfc.dat
echo This is not an empty file. Generated by ransom-prevention.bat > %windir%\perfc
icacls "%windir%\perfc.dll" /inheritance:r /remove *S-1-5-32-544
icacls "%windir%\perfc.dat" /inheritance:r /remove *S-1-5-32-544
icacls "%windir%\perfc" /inheritance:r /remove *S-1-5-32-544

goto FINISHED

:REMOVE
:: BadRabbit
icacls "%windir%\cscc.dat" /grant *S-1-5-32-544:F
icacls "%windir%\infpub.dat" /grant *S-1-5-32-544:F
del %windir%\cscc.dat
del %windir%\infpub.dat
:: Petya and NotPetya
icacls "%windir%\perfc.dll" /grant *S-1-5-32-544:F
icacls "%windir%\perfc.dat" /grant *S-1-5-32-544:F
icacls "%windir%\perfc" /grant *S-1-5-32-544:F
del %windir%\perfc.dll
del %windir%\perfc.dat
del %windir%\perfc

:FINISHED
if errorlevel 1 goto error
COLOR 0A
cls
echo.
echo Done! All processes have completed successfully.
echo.
echo Press any key to exit...
pause>NUL
exit

:error
COLOR 0C
cls
echo.
echo An error occured!
echo Oops, something went wrong! Please check your permissions and try again.
echo.
echo Press any key to exit...
pause>NUL
exit

:unrecog
cls
COLOR 0C
echo.
echo ^>^> Bad usage. You have to use one of the available arguments.
echo.
echo Press any key to restart...
pause>NUL
goto start
